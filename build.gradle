plugins {
    id("java")
    id("maven-publish")
    id("signing")
    id("net.auoeke.gronk").version("3.+")
    id("com.github.johnrengelman.shadow").version("latest.release")
}

final testProject = file("test/project").toPath()

version("0.3.7")
description("a javac plugin that disables exception checking and other bothersome restrictions")
javaVersion(17)

allprojects {
    group("net.auoeke")

    repositories {
        mavenCentral()
    }
}

gronk {
	export(sourceSets.main, com.sun.source.util.Plugin.class.module.packages.collect {"jdk.compiler/" + it})
}

sourceSets {
    test {
        java.srcDirs = [testProject.resolve("source")]
    }
}

dependencies {
    annotationProcessor("net.auoeke:uncheck-all")
    implementation("net.auoeke:reflect")
    implementation("org.ow2.asm:asm-tree")

    testAnnotationProcessor(rootProject)
    testImplementation(gradleTestKit())
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

java {
    withJavadocJar()
}

tasks.withType(Jar) {
    manifest.attributes("Automatic-Module-Name": "uncheck")
}

shadowJar {
    assemble.dependsOn(it)

	archiveAppendix = archiveClassifier.get()
    archiveClassifier = null

	relocate("net.auoeke.reflect", "net.auoeke.uncheck.reflect")
	relocate("net.auoeke.result", "net.auoeke.uncheck.result")
	relocate("net.gudenau.lib.unsafe", "net.auoeke.uncheck.unsafe")
	relocate("org.objectweb.asm", "net.auoeke.uncheck.asm")
}

test {
    dependsOn(publishToMavenLocal)
    mustRunAfter(clean)
    useJUnitPlatform()
}

components.java {
    withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
        // Exclude the fat JAR from the main publication.
        skip()
    }
}

publishing {
    repositories {
	    maven(testProject.resolve("build/repository")) {
		    name = "test"
	    }

        maven(findProperty("maven.repository")) {
            username(findProperty("maven.username"))
            password(findProperty("maven.password"))
        }
    }

    publications {
        main(MavenPublication) {
            from(components.java)
        }

        fat(MavenPublication) {
            shadow.component(fat)

            groupId(project.group)
            artifactId("$project.name-" + shadowJar.archiveAppendix.get())
            version(project.version)

            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }

    publications.withType(MavenPublication) {
        pom {
	        url = "https://github.com/auoeke/uncheck"

            licenses {
                license {
                    name = "MIT"
                    url = "https://opensource.org/licenses/MIT"
                }
            }

            developers {
                developer {
                    id = "auoeke"
                    email = "tjmnkrajyej@gmail.com"
                }
            }

            scm {
	            url = "https://github.com/auoeke/uncheck/tree/master"
	            connection = "scm:git:git://github.com/auoeke/uncheck.git"
	            developerConnection = "scm:git:git://github.com/auoeke/uncheck.git"
            }
        }
    }
}
